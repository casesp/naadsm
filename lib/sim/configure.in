dnl Process this file with autoconf to produce a configure script.
AC_INIT(sim, 3.1.18)
AC_CONFIG_SRCDIR(src/main.c)
AM_INIT_AUTOMAKE
AM_CONFIG_HEADER(config.h)

dnl Adds command-line options.

AC_ARG_ENABLE([debug],
  AC_HELP_STRING([--enable-debug], [provide an option for debug output]),
  [ac_cv_enable_debug=${enable_debug}],
  AC_CACHE_CHECK([whether enable-debug was specified], ac_cv_enable_debug,
    ac_cv_enable_debug=yes)
) # end of AC_ARG_ENABLE debug
case "X${ac_cv_enable_debug}" in
  Xyes|X )
    AC_DEFINE(DEBUG,1,[Define to 1 to provide an option for debug output.]) ;;
  * )
    ;;
  esac

AC_ARG_ENABLE([info],
  AC_HELP_STRING([--enable-info], [provide an option for informational output]),
  [ac_cv_enable_info=${enable_info}],
  AC_CACHE_CHECK([whether enable-info was specified], ac_cv_enable_info,
    ac_cv_enable_info=yes)
) # end of AC_ARG_ENABLE info
case "X${ac_cv_enable_info}" in
  Xyes|X )
    AC_DEFINE(INFO,1,[Define to 1 to provide an option for informational output.]) ;;
  * )
    ;;
  esac

dnl Checks for programs.
AC_PROG_CC
AC_C_CONST

AM_PROG_LEX
AC_PROG_YACC

AC_LIBTOOL_DLOPEN
AC_LIBLTDL_CONVENIENCE
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL
AC_CONFIG_SUBDIRS(libltdl)
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)
DLPREOPEN="-dlpreopen force"
AC_SUBST(DLPREOPEN)

dnl Checks for libraries.

PKG_CHECK_MODULES(GLIB,glib >= 2.4,,try_glib_20=yes)
if test "X${try_glib_20}" = Xyes
then
  PKG_CHECK_MODULES(GLIB,glib-2.0 >= 2.4,,AC_MSG_ERROR(cannot continue without glib))
fi
AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

AM_PATH_GSL(1.4,[have_gsl=true],AC_MSG_ERROR(cannot continue without GSL))
AC_SUBST(GSL_CFLAGS)
AC_SUBST(GSL_LIBS)

AM_PATH_SPRNG(,AC_MSG_ERROR(cannot continue without SPRNG))
AC_SUBST(SPRNG_CFLAGS)
AC_SUBST(SPRNG_LIBS)

AC_CHECK_LIB(popt,poptGetNextOpt,,AC_MSG_ERROR(cannot continue without popt))

AC_CHECK_LIB(m,pow,,AC_MSG_ERROR(cannot continue without math library))

AC_CHECK_LIB(rtree,RTreeNewIndex,,AC_MSG_ERROR(cannot continue without R-tree library))

AC_CHECK_LIB(shp,SHPCreate,,AC_MSG_ERROR(cannot continue without Shapefile library))

AC_CHECK_LIB(gpcl,gpc_polygon_clip,,AC_MSG_ERROR(cannot continue without General Polygon Clipping (GPC) library))

AC_CHECK_LIB(gd,gdImageCreate,,AC_MSG_ERROR(cannot continue without GD graphics drawing library))

AM_PATH_MPI()
AC_SUBST(MPI_CFLAGS)
AC_SUBST(MPI_LIBS)
AM_CONDITIONAL(HAVE_MPI, test "X${ac_cv_with_mpi}" != Xno)

PKG_CHECK_MODULES(SCEW,scew >= 0.3.1,,AC_MSG_ERROR(cannot continue without SCEW))
AC_SUBST(SCEW_CFLAGS)
AC_SUBST(SCEW_LIBS)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(errno.h math.h unistd.h ltdl.h regex.h ctype.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_CHECK_FUNCS(strdup strstr strcasecmp strtod strtol atan2)
AC_REPLACE_FUNCS(memchr strnlen getstr getline g_strsplit_regexp)

dnl Note that we don't do a text substitution to create LTLIBOBJS from LIBOBJS,
dnl as instructed in sec. 12.1 of "GNU Autoconf, Automake, and Libtool".  This
dnl function is now handled by Autoconf.  See "AC_LIBOBJ vs. LIBOBJS", sec.
dnl 15.6.4 of the Autoconf manual.

AC_OUTPUT(Makefile \
  replace/Makefile \
  rng/Makefile \
  wml/Makefile \
  wml/test/Makefile \
  herd/Makefile \
  herd/test/Makefile \
  event/Makefile \
  prob_dist/Makefile \
  prob_dist/test/Makefile \
  rel_chart/Makefile \
  rel_chart/test/Makefile \
  gis/Makefile \
  gis/test/Makefile \
  reporting/Makefile \
  reporting/test/Makefile \
  zone/Makefile \
  src/Makefile \
  models/Makefile \
  models/test/Makefile \
  models/test/model.disease/Makefile \
  models/test/model.direct-contact-spread/Makefile \
  models/test/model.indirect-contact-spread/Makefile \
  models/test/model.airborne-spread/Makefile \
  models/test/model.detection/Makefile \
  models/test/model.control-measures/Makefile \
  models/test/model.ring-destruction/Makefile \
  models/test/model.ring-vaccination/Makefile \
  models/test/model.zones/Makefile \
  smherds2xml/Makefile \
  sm3herds2xml/Makefile \
  xml2sm3herds/Makefile \
  filters/Makefile \
  doc/Makefile \
  doc/diagrams/Makefile)
